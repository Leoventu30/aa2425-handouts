plugins {
  id 'java'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
  mavenCentral()
}

dependencies {
  testImplementation('org.junit.jupiter:junit-jupiter:5.9.2')
  testRuntimeOnly('org.junit.platform:junit-platform-launcher')
  testImplementation('org.hamcrest:hamcrest:2.2')
}

test {
  useJUnitPlatform()
  testLogging {
    events 'passed', 'skipped', 'failed'
  }
  defaultCharacterEncoding = 'UTF-8'
}

tasks.withType(JavaExec) {
  enableAssertions = project.hasProperty('enableAssertions') ? project.getProperty('enableAssertions') : false
}

task runClass(type: JavaExec) {
  dependsOn build
  group = 'Execution'
  description = 'Runs the main method of a class given its fully qualified name.'
  mainClass = project.hasProperty('mainClass') ? project.getProperty('mainClass') : 'my.pkg.Example'
  classpath = sourceSets.main.runtimeClasspath
  defaultCharacterEncoding = 'UTF-8'
}

javadoc {
  options {
    encoding = 'UTF-8'
    charSet = 'UTF-8'
    links = [ 'https://docs.oracle.com/en/java/javase/17/docs/api/' ]
    docTitle = 'Programmazione II @ UniMI'
    overview = 'src/overview.html'
    header = '<script type=\'text/javascript\' src=\'https://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS-MML_HTMLorMML\'></script>'
    showFromPrivate()
    addStringOption 'Xdoclint:none', '-quiet'
    addBooleanOption '-allow-script-in-comments', true 
  }
}

